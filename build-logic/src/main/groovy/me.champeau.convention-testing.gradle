plugins {
    id 'java'
}

import org.gradle.util.GradleVersion

def wrapperVersion = GradleVersion.current().version

// The plugin is broken with Gradle 5.6.*
def otherVersions = ['6.9.1', '7.2', '7.3', '7.4']

def testedGradleVersions = (otherVersions - wrapperVersion) as SortedSet

def test = tasks.named("test")
def check = tasks.named("check")

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.named("test") {
    description = "Runs the tests against Gradle $wrapperVersion"
    systemProperty "gradleVersion", wrapperVersion
}

def testAllGradleVersions = tasks.register("testAllGradleVersions") {
    group = "verification"
    description = "Runs the tests against all supported Gradle versions"
    dependsOn(test)
}

check.configure {
    dependsOn(testAllGradleVersions)
}

testedGradleVersions.each { gradleVersion ->
    def task = tasks.register("testGradle${gradleVersion.replace(".", "_").replace("-", "_")}", Test) {
        group = "verification"
        description = "Runs the tests against Gradle $gradleVersion"
        classpath = test.get().classpath
        systemProperty "gradleVersion", gradleVersion
    }
    testAllGradleVersions.configure {
        dependsOn(task)
    }
}
