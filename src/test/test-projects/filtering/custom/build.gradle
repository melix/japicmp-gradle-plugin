import japicmp.filter.BehaviorFilter;
import japicmp.filter.FieldFilter;

import javassist.CtBehavior;
import javassist.CtField;
import javassist.Modifier;

plugins {
    id 'java'
    id 'me.champeau.gradle.japicmp'
}

repositories {
    mavenCentral()
}

sourceSets {
    main2
}

task jarv2(type: Jar) {
    archiveClassifier = 'v2'
    from sourceSets.main2.output
}


task japicmpAddCustomMatchTriggers(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath.from(jar)
    newClasspath.from(jarv2)
    txtOutputFile = layout.buildDirectory.file('reports/japi.txt').get().asFile
    addExcludeFilter(MyCustomFilter)
}


task japicmpRemoveCustomMatchTriggers(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath.from(jarv2)
    newClasspath.from(jar)
    txtOutputFile = layout.buildDirectory.file('reports/japi.txt').get().asFile
    addExcludeFilter(MyCustomFilter)
}


/**
 * Custom JAPICMP Filter that excludes volatile fields and synchronized methods.
 */
class MyCustomFilter implements FieldFilter, BehaviorFilter {

    @Override
    boolean matches(CtField ctField) {
        return Modifier.isVolatile(ctField.getModifiers())
    }

    @Override
    boolean matches(CtBehavior ctBehavior) {
        return Modifier.isSynchronized(ctBehavior.getModifiers())
    }
}
