apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

sourceCompatibility=1.7
targetCompatibility=1.7

compileGroovy.options.encoding = 'UTF-8'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

repositories {
    jcenter()
}

dependencies {
    compile('com.github.siom79.japicmp:japicmp:0.14.0') {
        exclude group:'com.google.guava'
        exclude group:'io.airlift'
        exclude group:'javax.xml.bind'
        exclude group:'com.sun.xml.bind'
    }

    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group:'org.codehaus.groovy'
    }
}


tasks.withType(JavaCompile) {
    options.incremental = true
}

test {
    inputs.dir("src/test/test-projects")
            .withPropertyName('testProjects')
}